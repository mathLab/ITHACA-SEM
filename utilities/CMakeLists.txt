MACRO(ADD_UTILITIES_EXECUTABLE name)
    CMAKE_PARSE_ARGUMENTS(UTIL "" "COMPONENT" "SOURCES;DEPENDS" ${ARGN})

    IF(NOT UTIL_DEPENDS)
        SET(UTIL_DEPENDS MultiRegions)
    ENDIF()

    ADD_NEKTAR_EXECUTABLE(${name}
        COMPONENT ${UTIL_COMPONENT} DEPENDS ${UTIL_DEPENDS} SOURCES ${UTIL_SOURCES})
ENDMACRO()

INCLUDE_DIRECTORIES(${CMAKE_SOURCE_DIR}/library ${CMAKE_SOURCE_DIR}/utilities)

CMAKE_DEPENDENT_OPTION(NEKTAR_UTILITY_NEKMESH
    "Build the NekMesh pre-processing utility." ON
    "NEKTAR_BUILD_UTILITIES" OFF)
CMAKE_DEPENDENT_OPTION(NEKTAR_UTILITY_FIELDCONVERT
    "Build the FieldConvert post-processing utility." ON
    "NEKTAR_BUILD_UTILITIES" OFF)

IF (IS_DIRECTORY Extras)
    CMAKE_DEPENDENT_OPTION(NEKTAR_UTILITY_EXTRAS
        "Build extra Nektar++ utilities." OFF
        "NEKTAR_BUILD_UTILITIES" OFF)
ENDIF()

IF (NOT NEKTAR_BUILD_UTILITIES)
    SET(NEKTAR_UTILITY_NEKMESH OFF CACHE INTERNAL "")
    SET(NEKTAR_UTILITY_FIELDCONVERT OFF CACHE INTERNAL "")
    SET(NEKTAR_UTILITY_EXTRAS OFF CACHE INTERNAL "")
ENDIF()

# Generate list of available utilities
IF (NEKTAR_UTILITY_NEKMESH)
    SUBDIRS(NekMesh)
ENDIF()

IF (NEKTAR_UTILITY_FIELDCONVERT)
    SUBDIRS(FieldConvert)
ENDIF()

IF (NEKTAR_UTILITY_EXTRAS)
    SUBDIRS(Extras)
ENDIF()
